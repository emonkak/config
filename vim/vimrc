" My vimrc
" Basic  {{{1
" Absolute  {{{2

if has('vim_starting')
  if has('nvim') && isdirectory('/usr/share/vim/vimfiles')
    set runtimepath^=/usr/share/vim/vimfiles
    set runtimepath+=/usr/share/vim/vimfiles/after
  endif
endif

set ambiwidth=single
set fileencodings=ucs-bom,utf-8,iso-2022-jp,euc-jp,cp932
set fileformats=unix,dos,mac

filetype plugin indent on

function! s:SID() abort
  return matchstr(get(function('s:SID'), 'name'), '^<SNR>\zs\d\+')
endfunction
let s:SID_PREFIX = '<SNR>' . s:SID() . '_'

" Syntax  {{{2

let g:ansi_colors = {
\   'background': '#23262e',
\   'foreground': '#d5dae9',
\   'black': '#38425a',
\   'red': '#cf6950',
\   'green': '#1c9969',
\   'yellow': '#aa7f2c',
\   'blue': '#8075f5',
\   'magenta': '#d95a88',
\   'cyan': '#3790b3',
\   'white': '#7486b2',
\   'bright-black': '#465476',
\   'bright-red': '#e9b5a8',
\   'bright-green': '#8dcfad',
\   'bright-yellow': '#d9bd95',
\   'bright-blue': '#c5b8fb',
\   'bright-magenta': '#f1afc2',
\   'bright-cyan': '#9cc7dd',
\   'bright-white': '#b8c0da',
\ }

if (1 < &t_Co || has('gui')) && has('syntax') && !exists('g:syntax_on')
  syntax enable
  if !exists('g:colors_name')
    colorscheme ansi_colors
  endif
endif

" Options  {{{2

if has('gui_running')
  if has('gui_gtk')
    set guifont=Monospace\ 10
    set linespace=0
  elseif has('gui_macvim')
    set guifont=Monaco:h12.5
    set linespace=2
    set macmeta
    set visualbell
  elseif has('gui_win32')
    set guifont=Consolas:h10.5
    set guifontwide=Meiryo:h10.5
    set renderoptions=type:directx,renmode:5
    set linespace=1
  endif
  set guicursor=a:blinkwait4000-blinkon1500-blinkoff500
  set guioptions=AcgM
endif

set autoindent
set backspace=indent,eol,start
if exists('+breakindent')
  set breakindent
endif
set cinoptions=l1,g0,t0,W1s
if exists('+clipboard')
  set clipboard-=autoselect
endif
set cmdheight=1
set complete-=i complete-=t
set completeopt=menuone,longest
set concealcursor=nc
set conceallevel=0
set confirm
set diffopt=filler,vertical
if &directory[0] ==# '.'
  set directory=~/.local/state/vim/swap
endif
set display=lastline
set noequalalways
set foldmethod=marker
set formatlistpat&
let &formatlistpat .= '\|^\s*[*+-]\s*'
set formatoptions=roqnlmM1
set grepformat=%f:%l:%c:%m
set hidden
set history=1000
set ignorecase
set incsearch
if exists('+inccommand')
  set inccommand=split
endif
set keywordprg=:help
if exists('+langremap')
  set nolangremap
endif
set laststatus=2
set linebreak
set list
let &listchars= "tab:>\u00b7,trail:\u00b7,extends:>,precedes:<,conceal:\u00a6,nbsp:+"
set mouse=
set nrformats-=octal
set pumheight=12
set scrolloff=5
if exists('+shellslash')
  set shellslash
endif
set shiftround
set showcmd
set smartcase
set splitbelow
set splitright
set synmaxcol=1000
if exists('+tagcase')
  set tagcase=match
endif
if exists('+termguicolors')
  set notermguicolors
endif
set title
set titlestring=%{v:progname}:\ %f%(\ %h%r%m%)
set ttimeoutlen=50
if &undodir ==# '.'
  set undodir=~/.local/state/vim/undo
endif
set undofile
set updatetime=1000
if exists('+shada')
  set shada^='1000 shada-='100
else
  set viminfo^='1000 viminfo-='100
  set viminfofile=~/.local/state/vim/viminfo
endif
set virtualedit=block
set wildmenu
set wildmode=full
if exists('+winborder')
  set winborder=rounded
endif
set nowrapscan
set nowritebackup

" Status-line  {{{2

let &statusline = '%<%f %h%m%r%w'
\               . '%='
\               . '['
\               . '%{&l:fileencoding == "" ? &encoding : &l:fileencoding}'
\               . '%{&l:bomb ? "/BOM" : ""}'
\               . ']'
\               . '[%{&l:fileformat}]'
\               . '   %-14.(%l,%c%V%) %P'

" Tab-line  {{{2

function! s:my_tabline() abort
  let s = ''

  for i in range(1, tabpagenr('$'))
    let bufnrs = tabpagebuflist(i)
    let curbufnr = bufnrs[tabpagewinnr(i) - 1]

    let no = (i <= 10 ? i - 1 : '#')
    let mod = getbufvar(curbufnr, '&modified') ? '+' : ' '
    let title = gettabvar(i, 'title')
    let title = title != '' ? title : fnamemodify(bufname(curbufnr), ':t')
    let title = title != '' ? title : '[No Name]'

    let s .= '%' . i . 'T'
    let s .= '%#' . (i == tabpagenr() ? 'TabLineSel' : 'TabLine') . '#'
    let s .= no
    let s .= mod
    let s .= title
    let s .= '%#TabLineFill#'
    let s .= '  '
  endfor

  let s .= '%#TabLineFill#%T'
  let s .= '%=%#TabLine#'
  let s .= '| %<'
  let s .= fnamemodify(getcwd(), ':t')
  return s
endfunction

set showtabline=1
let &tabline = '%!' . s:SID_PREFIX . 'my_tabline()'

" Packages  {{{2

if has('nvim')
  packadd nvim-treesitter
  packadd nvim-treesitter-textobjects
else
  if !has('patch-8.2.4275')
    " Add all "start" packages to 'runtimepath' explicitly, because Vim without
    " this patch does not autoload sources from 'packpath'.
    let &runtimepath .= ','
    \                . join(globpath(&packpath, 'pack/*/start/*', 0, 1), ',')
  endif

  packadd vim-altercmd
  packadd vim-arpeggio
endif

" Misc.  {{{2

augroup MyAutoCmd
  autocmd!
augroup END

let g:mapleader = ','
let g:maplocalleader = '.'

call altercmd#load()
call arpeggio#load()

" Utilities  {{{1
function! s:readdir(dir) abort  "{{{2
  if exists('*readdir')
    try
      return readdir(a:dir)
    catch /\<E484:/
      return []
    endtry
  else
    let paths = []
    call extend(paths, globpath(a:dir, '.*', 1, 1))
    call extend(paths, globpath(a:dir, '*', 1, 1))
    call map(paths, 'fnamemodify(v:val, ":t")')
    call filter(paths, 'v:val !~# "^\\.\\{1,2}$"')
    return paths
  endif
endfunction

function! s:sandbox_eval(expr) abort  "{{{2
  sandbox return eval(a:expr)
endfunction

function! s:trim(text) abort  "{{{2
  if exists('*trim')
    return trim(a:text)
  else
    return substitute(a:text, '^\s*\|\s*$', '', 'g')
  endif
endfunction

" Commands  {{{1
" :edit with specified 'fileencoding'  {{{2

command! -bang -bar -complete=file -nargs=? Cp932
\ edit<bang> ++enc=cp932 <args>
command! -bang -bar -complete=file -nargs=? Eucjp
\ edit<bang> ++enc=euc-jp <args>
command! -bang -bar -complete=file -nargs=? Iso2022jp
\ edit<bang> ++enc=iso-2022-jp <args>
command! -bang -bar -complete=file -nargs=? Utf8
\ edit<bang> ++enc=utf-8 <args>
command! -bang -bar -complete=file -nargs=? Utf16be
\ edit<bang> ++enc=utf-16be <args>
command! -bang -bar -complete=file -nargs=? Utf16
\ edit<bang> ++enc=utf-16le <args>
command! -bang -bar -complete=file -nargs=? Utf32be
\ edit<bang> ++enc=utf-32be <args>
command! -bang -bar -complete=file -nargs=? Utf32
\ edit<bang> ++enc=utf-32le <args>

command! -bang -bar -complete=file -nargs=? Dos
\ edit<bang> ++ff=dos <args>
command! -bang -bar -complete=file -nargs=? Unix
\ edit<bang> ++ff=unix <args>
command! -bang -bar -complete=file -nargs=? Mac
\ edit<bang> ++ff=mac <args>

" :grep wrappers  {{{2

command! -complete=customlist,s:complete_Grep -nargs=+ Grep
\ call s:cmd_Grep('grep', 'cwindow', <f-args>)
command! -complete=customlist,s:complete_Grep -nargs=+ Lgrep
\ call s:cmd_Grep('lgrep', 'lwindow', <f-args>)

function! s:cmd_Grep(command, window_command, ...) abort
  let pattern = escape(shellescape(a:000[-1]), '#%|')
  let target = a:0 > 1
  \          ? escape(shellescape(join(a:000[:-2], ' ')), '|')
  \          : ''
  execute (a:command . '!') pattern target
  execute a:window_command
endfunction

function! s:complete_Grep(arglead, cmdline, cursorpos) abort
  return glob(join(split(a:cmdline, '\s', 1)[1:], ' ') . '*', 0, 1)
endfunction

AlterCommand gr[ep]  Grep
AlterCommand lgr[ep]  LGrep

let s:GREPPRGS = [
\   executable('rg') ? 'rg --vimgrep --no-heading' : 'grep -nHE',
\   'git grep --line-number --column',
\ ]

function! s:toggle_grepprg() abort
  let i = (index(s:GREPPRGS, &l:grepprg) + 1) % len(s:GREPPRGS)
  let &l:grepprg = s:GREPPRGS[i]
  echo &l:grepprg
endfunction

function! s:detect_greprg() abort
  let git_dir = finddir('.git', expand('%:p:h') . ';')
  return s:GREPPRGS[git_dir != '' ? 1 : 0]
endfunction

autocmd MyAutoCmd BufEnter *
\   if &l:grepprg == ''
\ |   let &l:grepprg = s:detect_greprg()
\ | endif

" :make wrappers  {{{2

command! -complete=file -nargs=* Make
\ call s:make('make', 'cwindow', [<f-args>])
command! -complete=file -nargs=* Lmake
\ call s:make('lmake', 'lwindow', [<f-args>])

function! s:make(command, window_command, args) abort
  let original_winnr = winnr()

  execute a:command . '!' join(a:args)
  execute a:window_command

  execute original_winnr 'wincmd w'
endfunction

AlterCommand mak[e]  Make
AlterCommand lmak[e]  Lmake

" BufExecute  {{{2

command! -nargs=+ -complete=file BufExecute  call s:cmd_BufExecute(<f-args>)

function! s:cmd_BufExecute(...) abort
  if a:0 > 1
    let path = substitute(a:1, '\\\@<!\~', $HOME, 'g')
    let pattern = glob2regpat(path)
    let command = join(a:000[1:], ' ')
    let search_by_root = path[0] ==# '/'
  else
    let pattern = '.*'
    let command = join(a:000, ' ')
    let search_by_root = 0
  endif
  let original_bufnr = bufnr('%')
  let execute_count = 0
  try
    for buffer in getbufinfo({ 'buflisted': 1 })
      let relative_name = fnamemodify(buffer.name, ':.')
      if (search_by_root || buffer.name !=# relative_name)
      \  && relative_name =~# pattern
        execute buffer.bufnr 'bufdo' command
        let execute_count += 1
      endif
    endfor
  finally
    execute original_bufnr 'buffer!'
    echo 'Execute ":' . command . '" in ' . execute_count . ' buffer(s)'
  endtry
endfunction

" BufSweap  {{{2

command! -bang -bar -nargs=0 BufSweap  call s:cmd_BufSweap(<bang>0)

function! s:cmd_BufSweap(banged) abort
  let bufnrs = range(1, bufnr('$'))
  call filter(bufnrs, 'bufexists(v:val)
  \                 && buflisted(v:val)
  \                 && (bufname(v:val) == "" || !filereadable(bufname(v:val)))
  \                 && (a:banged || !getbufvar(v:val, "&modified"))')
  for bufnr in bufnrs
    silent execute bufnr 'bwipeout' . (a:banged ? '!' : '')
  endfor
  echo len(bufnrs) 'buffer(s) are deleted'
endfunction

" CD  {{{2

command! -bang -bar -nargs=* -complete=customlist,s:complete_CD CD
\ call s:cmd_CD(exists(':tcd') ? 'tcd' : 'lcd', <q-args>)

function! s:cmd_CD(cd_command, path) abort
  if a:path != ''
    execute a:cd_command fnameescape(a:path)
  else
    let buffer_dir = expand('%:p:h')
    if isdirectory(buffer_dir)
      let git_dir = finddir('.git', buffer_dir . ';')
      let new_cwd = git_dir != ''
      \ ? fnamemodify(git_dir, ':p:h:h')
      \ : buffer_dir
      execute a:cd_command fnameescape(new_cwd)
    else
      execute a:cd_command
    endif
  endif
  let t:cwd = getcwd(-1)
  pwd
endfunction

function! s:complete_CD(arglead, cmdline, cursorpos) abort
  return map(globpath(substitute(&cdpath, '^,', '', ''),
  \                   join(split(a:cmdline, '\s', 1)[1:], ' ') . '*/',
  \                   0,
  \                   1),
  \          'v:val[:-2]')
endfunction

AlterCommand cd  CD

if exists(':tcd')
  autocmd MyAutoCmd BufEnter ?*
  \   if !exists('t:cwd') && getbufvar('%', '&buftype') == ''
  \ |   silent CD
  \ | endif
endif

" FoldDump  {{{2

command! -bar -range=% FoldDump
\ <line1>,<line2>global/^/echo
\ printf("%*d [%2s] %s",
\   len(line('$')),
\   line('.'),
\   s:eval_foldexpr(&l:foldexpr, line('.')),
\   getline('.')
\ ) | nohlsearch

function s:eval_foldexpr(foldexpr, lnum)
  let v:lnum = a:lnum
  return eval(a:foldexpr)
endfunction

" Indent styles  {{{2

command! -bar -nargs=1 SpaceIndent
\ setlocal expandtab shiftwidth=<args> softtabstop=<args> tabstop<

command! -bar -nargs=1 TabIndent
\ setlocal noexpandtab shiftwidth< softtabstop< tabstop=<args>

" InspectSyntax {{{2

command! -bar -nargs=0 InspectSyntax
\ echo join(<SID>inspect_syntax(line('.'), col('.')), '/')

function! s:inspect_syntax(line, col) abort
  let stack = []

  for syn_id in synstack(a:line, a:col)
    let syntax_name = synIDattr(syn_id, 'name')
    let syn_trans_id = synIDtrans(syn_id)

    if syn_id != syn_trans_id
      let syntax_name .= '<' . synIDattr(syn_trans_id, 'name') . '>'
    endif

    call add(stack, syntax_name)
  endfor

  return stack
endfunction

" Note  {{{2

command! -bar -nargs=? Note
\ execute printf(
\   'edit %s/Sync/Documents/Notes/%s%s.md',
\   $HOME,
\   strftime('%Y-%m-%d'),
\   <q-args> != '' ? '_' . <q-args> : ''
\ )

" TableMake  {{{2

command! -bar -nargs=? -range TableBreak
\ <line1>,<line2>call s:cmd_TableBreak(<q-args>)
command! -bar -nargs=? -range TableMake
\ <line1>,<line2>call s:cmd_TableMake(<q-args>)

function s:cmd_TableBreak(separator) abort range
  let HEADER_PATTERN = '^|\%(\s*:\?-\+:\?\s*|\)\+$'
  let ROW_PATTERN = '^|.*|$'
  let BORDER_PATTERN = '\s*|\s*'
  let separator = a:separator != '' ? a:separator : "\t"
  let rows = []
  for lnum in range(a:firstline, a:lastline)
    let line = getline(lnum)
    if line =~# HEADER_PATTERN
      continue
    endif
    if line =~# ROW_PATTERN
      let row = join(split(s:trim(line[1:-2]), BORDER_PATTERN), separator)
      call add(rows, row)
    else
      call add(rows, line)
    endif
  endfor
  call setline(a:firstline, rows)
  if len(rows) < a:lastline - a:firstline + 1
    let bufnr = bufnr('%')
    call deletebufline(bufnr, a:firstline + len(rows), a:lastline)
  endif
endfunction

function s:cmd_TableMake(pattern) abort range
  let pattern = a:pattern != '' ? a:pattern : '\t'
  let rows = map(getline(a:firstline, a:lastline), 'split(v:val, pattern)')
  if len(rows) == 0
    return
  endif
  let column_widths = map(copy(rows[0]), 'strdisplaywidth(v:val)')
  for row in rows[1:]
    for i in range(min([len(row), len(column_widths)]))
      let column_widths[i] = max([column_widths[i], strdisplaywidth(row[i])])
    endfor
  endfor
  let header = ''
  let body = []
  for column_width in column_widths
    let header .= '| ' . repeat('-', column_width) . ' '
  endfor
  let header .= '|'
  for row in rows
    let line = ''
    for i in range(max([len(row), len(column_widths)]))
      let cell = get(row, i, '')
      let spaces = i < len(column_widths)
      \          ? repeat(' ', column_widths[i] - strdisplaywidth(cell))
      \          : ''
      let line .= '| ' . cell . spaces . ' '
    endfor
    let line .= '|'
    call add(body, line)
  endfor
  call setline(a:firstline, body)
  call append(a:firstline - 1, header)
endfunction

" Pretty-Printing  {{{2

command! -nargs=? -range PrettyJson
\   if <q-args> != ''
\ |   echo s:pretty_print(json_decode(s:trim(<q-args>)), 0, 2, function('json_encode'))
\ | else
\ |   call s:prettify(visualmode(), 2, function('json_decode'), function('json_encode'))
\ | endif

command! -nargs=? -range PrettyEval
\   if <q-args> != ''
\ |   echo s:pretty_print(s:sandbox_eval(s:trim(<q-args>)), 0, 2, function('string'))
\ | else
\ |   call s:prettify(visualmode(), 2, function('s:sandbox_eval'), function('string'))
\ | endif

function! s:prettify(visual_command, indent_width, Decode, Encode) abort
  let reg_value = getreg('"')
  let reg_type = getregtype('"')
  try
    execute 'normal!' ('`<' . a:visual_command . '`>""y')
    let @" = s:pretty_print(a:Decode(s:trim(@")), 0, a:indent_width, a:Encode)
    execute 'normal!' ('`<' . a:visual_command . '`>""p')
  finally
    call setreg('"', reg_value, reg_type)
  endtry
endfunction

function! s:pretty_print(value, depth, indent_width, Encode) abort
  let type = type(a:value)
  if type is v:t_list
    if empty(a:value)
      return '[]'
    endif
    let current_indent = repeat(' ', a:depth * a:indent_width)
    let next_indent = repeat(' ', (a:depth + 1) * a:indent_width)
    let len = len(a:value)
    let values = map(
    \   a:value,
    \   { i, value -> next_indent
    \                 . s:pretty_print(value, a:depth + 1, a:indent_width, a:Encode)
    \                 . (i + 1 == len ? '' : ',')
    \   }
    \ )
    return "[\n"
    \      . join(values, "\n") . "\n"
    \      . current_indent . ']'
  elseif type is v:t_dict
    if empty(a:value)
      return '{}'
    endif
    let current_indent = repeat(' ', a:depth * a:indent_width)
    let next_indent = repeat(' ', (a:depth + 1) * a:indent_width)
    let len = len(a:value)
    let values = map(
    \   items(a:value),
    \   { i, val ->
    \     next_indent . a:Encode(val[0]) . ': '
    \                 . s:pretty_print(val[1], a:depth + 1, a:indent_width, a:Encode)
    \                 . (i + 1 == len ? '' : ',')
    \   }
    \ )
    return "{\n"
    \      . join(values, "\n") . "\n"
    \      . current_indent . '}'
  else
    return a:Encode(a:value)
  endif
endfunction

" Remote commands  {{{2

command! -bar -nargs=1 -complete=customlist,s:complete_server_name
\ RemotePut read! ssh <args> pbpaste

command! -bar -nargs=1 -range -complete=customlist,s:complete_server_name
\ RemoteYank
\ call system('ssh ' . shellescape(<q-args>) . ' pbcopy', getline(<line1>, <line2>))

function! s:complete_server_name(arglead, cmdline, cursorpos) abort
  let path = expand('$HOME/.ssh/known_hosts')
  try
    let server_names = readfile(path, '')
  catch /\<E484:/
    return []
  endtry
  call map(server_names, { _, line -> matchstr(line, '^\S\+') })
  call sort(server_names)
  call uniq(server_names)
  call filter(server_names, { _, name -> stridx(name, a:arglead) == 0 })
  return server_names
endfunction

" Rename  {{{2

command! -bar -complete=file -nargs=1 Rename  call s:cmd_Rename(<q-args>)

function! s:cmd_Rename(new_path) abort
  let old_path = expand('%')
  if &l:readonly || (filereadable(old_path) && !filewritable(old_path))
    echohl ErrorMsg
    echo 'This buffer is read-only'
    echohl None
  elseif filereadable(a:new_path)
    echohl ErrorMsg
    echo 'A file already exists at the destination' string(a:new_path)
    echohl None
  else
    let new_dir = fnamemodify(a:new_path, ':p:h')
    if !isdirectory(new_dir)
      call mkdir(new_dir, 'p')
    endif
    file `=fnamemodify(a:new_path, ':.')`
    silent write
    call delete(old_path)
    let old_dir = fnamemodify(old_path, ':p:h')
    if empty(s:readdir(old_dir))
      call delete(old_dir, 'd')
    endif
    echo 'Renamed:' old_path '->' a:new_path
  endif
endfunction

" Reverse  {{{2

command! -bar -range=% Reverse
\   <line1>,<line2>global/^/m<line1>-1
\ | nohlsearch

" Source  {{{2

command! -bar -complete=file -nargs=? -range Source
\   echo 'Sourcing ...' expand(<q-args>)
\ | if <q-args> != ''
\ |   source <args>
\ | else
\ |   <line1>,<line2> source
\ | endif

AlterCommand so[urce]  Source

" Sum  {{{2

command! -bar -range -nargs=? Sum
\ <line1>,<line2>call s:sum_lines(<q-args>)

function! s:sum_lines(pattern) abort range
  let total = 0
  let numbers = []

  for lnum in range(a:firstline, a:lastline)
    let line = getline(lnum)
    if a:pattern !=# ''
      let line = matchstr(line, a:pattern)
    endif
    if line !=# ''
      let n = s:extract_number(line)
      let total += n
      call add(numbers, n)
    endif
  endfor

  echo join(numbers, ' + ') '=' total

  call append(a:lastline, string(total))
endfunction

function! s:extract_number(line) abort
  let n = matchstr(a:line,
  \                '\c\(0\(x\x\+\|b[01]\+\|o\?[0-7]\+\)\)\|\d\+\(\.\d\+\)\?')
  if n == ''
    return 0
  endif
  if stridx(n, '.') >= 0
    return str2float(n)
  endif
  if n[0:1] == '0x'
    let base = 16
  elseif n[0:1] == '0b'
    let base = 2
  elseif n[0] == '0'
    let base = 8
  else
    let base = 10
  endif
  return str2nr(n, base)
endfunction

" SuspendWithAutomticCD  {{{2

command! -bar -nargs=0 SuspendWithAutomticCD
\ call s:cmd_SuspendWithAutomticCD()

function! s:cmd_SuspendWithAutomticCD() abort
  if exists('$TMUX')
    let shell = split(&shell, '/')[-1]
    let select_command = 'new-window'
    let target_pane = ':$'
    for window in split(system('tmux list-windows'), '\n')
      let matches = matchlist(window, '^\(\d\+\):\s\(\w\+\)')
      if !empty(matches) && matches[2] ==# shell
        let select_command = 'select-window -t ' . matches[1]
        let target_pane = ':' . matches[1]
        break
      endif
    endfor
    silent execute printf(
    \  '!tmux %s \; send-keys -t %s C-u " cd \"%s\"" C-m',
    \   select_command,
    \   target_pane,
    \   getcwd()
    \ )
    redraw!
  else
    suspend
  endif
endfunction

" TabpageTitle  {{{2

command! -bar -nargs=* TabpageTitle
\   if <q-args> == ''
\ |   let t:title = input("Set tabpage's title to: ", '')
\ | else
\ |   let t:title = <q-args>
\ | endif
\ | redraw!

" Window helpers  {{{2

command! -bar -complete=file -nargs=* Split
\ Vertical split <args>
command! -bar -complete=file -nargs=* SplitTop
\ Vertical topleft split <args>
command! -bar -complete=file -nargs=* SplitBottom
\ Vertical botright split <args>
command! -bar -complete=help -nargs=* Help
\ Vertical help <args>
command! -bar -complete=file -nargs=* New
\ Vertical new <args>

command! -bar -nargs=* Vertical
\ execute (winwidth(0) > winheight(0) * 4 ? 'vertical' : '') <q-args>

AlterCommand sp[lit]  Split
AlterCommand h[elp]  Help
AlterCommand new  New

" YankMatchAll  "{{{2

command! -bar -range=% -register YankMatchAll
\ <line1>,<line2>call s:cmd_YankMatchAll(<q-reg>)

function! s:cmd_YankMatchAll(register) abort range
  let matches = []
  let pattern = @/
  for lnum in range(a:firstline, a:lastline)
    let line = getline(lnum)
    call extend(matches, s:match_all(line, pattern))
  endfor
  call setreg(a:register, join(matches, "\n"), 'l')
endfunction

function! s:match_all(text, pattern) abort
  let matches = []
  let cursor = 0
  while 1
    let start = match(a:text, a:pattern, cursor)
    if start < 0
      break
    endif
    let end = matchend(a:text, a:pattern, cursor)
    if end > start
      call add(matches, a:text[start:end - 1])
      let cursor = end
    else
      let cursor = end + 1
    endif
  endwhile
  return matches
endfunction

" Mappings  {{{1
" QuickFix  {{{2

noremap q  <Nop>
nnoremap Q  q

" For quickfix list.
nnoremap <silent> qj  <Cmd>cnext<CR>
nnoremap <silent> qk  <Cmd>cprevious<CR>
nnoremap <silent> qr  <Cmd>crewind<CR>
nnoremap <silent> qK  <Cmd>cfirst<CR>
nnoremap <silent> qJ  <Cmd>clast<CR>
nnoremap <silent> qfj  <Cmd>cnfile<CR>
nnoremap <silent> qfk  <Cmd>cpfile<CR>
nnoremap <silent> ql  <Cmd>clist<CR>
nnoremap <silent> qq  <Cmd>cc<CR>
nnoremap <silent> qo  <Cmd>copen<CR>
nnoremap <silent> qc  <Cmd>cclose<CR>
nnoremap <silent> qp  <Cmd>colder<CR>
nnoremap <silent> qn  <Cmd>cnewer<CR>
nnoremap <silent> qm  <Cmd>Make<CR>
nnoremap qM  :<C-u>Make<Space>
nnoremap q<Space>  :<C-u>Make<Space>
nnoremap qg  :<C-u>Grep<Space>

" For location list.
nnoremap <silent> qwj  <Cmd>lnext<CR>
nnoremap <silent> qwk  <Cmd>lprevious<CR>
nnoremap <silent> qwr  <Cmd>lrewind<CR>
nnoremap <silent> qwK  <Cmd>lfirst<CR>
nnoremap <silent> qwJ  <Cmd>llast<CR>
nnoremap <silent> qwfj  <Cmd>lnfile<CR>
nnoremap <silent> qwfk  <Cmd>lpfile<CR>
nnoremap <silent> qwl  <Cmd>llist<CR>
nnoremap <silent> qwq  <Cmd>ll<CR>
nnoremap <silent> qwo  <Cmd>lopen<CR>
nnoremap <silent> qwc  <Cmd>close<CR>
nnoremap <silent> qwp  <Cmd>lolder<CR>
nnoremap <silent> qwn  <Cmd>lnewer<CR>
nnoremap <silent> qwm  <Cmd>Lmake<CR>
nnoremap qwM  :<C-u>Lmake<Space>
nnoremap qw<Space>  :<C-u>Lmake<Space>
nnoremap qwg  :<C-u>LGrep<Space>

" Tab pages  {{{2

noremap <C-t>  <Nop>

nnoremap <silent> <C-t>n  <Cmd>tabnew \| :tabmove<CR>
nnoremap <silent> <C-t>c  <Cmd>tabclose<CR>
nnoremap <silent> <C-t>o  <Cmd>tabonly<CR>
nnoremap <silent> <C-t>i  <Cmd>tabs<CR>

nmap <C-t><C-n>  <C-t>n
nmap <C-t><C-c>  <C-t>c
nmap <C-t><C-o>  <C-t>o
nmap <C-t><C-i>  <C-t>i

nnoremap <silent> <C-t><Space>  <Cmd>TabpageTitle<CR>

nmap <silent> <C-t><C-@>  <C-t><Space>
nmap <silent> <C-t><C-Space>  <C-t><Space>

" Moving around tab pages.
nnoremap <silent> <C-t>j
\ <Cmd>execute 'tabnext' 1 + (tabpagenr() + v:count1 - 1) % tabpagenr('$')<CR>
nnoremap <silent> <C-t>k
\ <Cmd>execute 'tabprevious' v:count1 % tabpagenr('$')<CR>
nnoremap <silent> <C-t>K  <Cmd>tabfirst<CR>
nnoremap <silent> <C-t>J  <Cmd>tablast<CR>

nmap <C-t><C-j>  <C-t>j
nmap <C-t><C-k>  <C-t>k
nmap <C-t><C-t>  <C-t>j

" Moveing the specific tag page.
for s:i in range(10)
  execute 'nnoremap <silent>' ('<C-t>'.(s:i))  ((s:i+1).'gt')
endfor
unlet s:i

" Moving tabpages them selves.
nnoremap <silent> <C-t>l  <Cmd>execute 'tabmove' '+'.v:count1<CR>
nnoremap <silent> <C-t>h  <Cmd>execute 'tabmove' '-'.v:count1<CR>
nnoremap <silent> <C-t>L  <Cmd>tabmove<CR>
nnoremap <silent> <C-t>H  <Cmd>tabmove 0<CR>

nmap <C-t><C-l>  <C-t>l
nmap <C-t><C-h>  <C-t>h

" Argument list  {{{2

noremap <C-g>  <Nop>

nnoremap <silent> <C-g>l  <Cmd>args<CR>
nnoremap <silent> <C-g>j  <Cmd>next<CR>
nnoremap <silent> <C-g>k  <Cmd>previous<CR>
nnoremap <silent> <C-g>J  <Cmd>last<CR>
nnoremap <silent> <C-g>K  <Cmd>first<CR>
nnoremap <silent> <C-g>wj  <Cmd>wnext<CR>
nnoremap <silent> <C-g>wk  <Cmd>wprevious<CR>
nnoremap <C-g><Space>  :<C-u>args<Space>

nmap <C-g><C-l>  <C-g>l
nmap <C-g><C-j>  <C-g>j
nmap <C-g><C-k>  <C-g>k
nmap <C-g><C-w><C-j>  <C-g>wj
nmap <C-g><C-w><C-k>  <C-g>wk
nmap <C-g><C-@>  <C-g><Space>
nmap <C-g><C-Space>  <C-g><Space>

" Command-line editing  {{{2

" Basic mappings like GNU Emacs.
cnoremap <C-b>  <Left>
cnoremap <C-f>  <Right>
cnoremap <C-a>  <Home>
cnoremap <C-d>  <Delete>
cnoremap <expr> <C-y>  pumvisible() ? "\<C-y>" : "\<C-r>\""
cnoremap <C-k>
\ <C-\>e getcmdpos() == 1 ? '' : getcmdline()[:getcmdpos()-2]<CR>
cnoremap <expr> <C-u>  getcmdline() == '' ? "\<C-c>" : "\<C-u>"
cnoremap <expr> <C-w>  getcmdline() == '' ? "\<C-c>" : "\<C-w>"

" Swap <C-p>, <C-n> to <Up>, <Down>.
cnoremap <C-p>  <Up>
cnoremap <C-n>  <Down>
cnoremap <Up>  <C-p>
cnoremap <Down>  <C-n>

" An alternative key to open the command-line history.
cnoremap <C-o>  <C-f>

" Escape the slash character when command-line type is 'search'.
cnoremap <expr> /  index(['/', '?'], getcmdtype()) >= 0 ? '\/' : '/'

" When you type "s<Space>", it's expanded to "%s//|/g".
cnoreabbrev <expr> s
\ [getcmdtype(), getcmdline()] ==# [':', 's']
\ ? '%s///g<Left><Left>' . getchar()[1:0]
\ : 's'

" Command-line window  {{{2

autocmd MyAutoCmd CmdwinEnter *
\ call s:on_CmdwinEnter()

function! s:on_CmdwinEnter() abort
  " Close the command-line window.
  nnoremap <buffer> <Esc>  <Esc><C-w>q

  " Close the command-line window if the current line is empty.
  inoremap <buffer> <expr> <BS>
         \ getline('.') == '' ? "\<C-c>\<C-c>" : col('.') == 1 ? '' : "\<BS>"
  inoremap <buffer> <expr> <C-w>
         \ getline('.') == '' ? "\<C-c>\<C-c>" : col('.') == 1 ? '' : "\<C-w>"
  inoremap <buffer> <expr> <C-u>
         \ getline('.') == '' ? "\<C-c>\<C-c>" : col('.') == 1 ? '' : "\<C-u>"

  imap <buffer> <C-h>  <BS>

  startinsert!
endfunction

" Insert mode  {{{2

" An alternative to <Esc>.
inoremap <C-@>  <Esc>
inoremap <C-Space>  <Esc>

" Basic mappings like GNU Emacs.
inoremap <C-b>  <Left>
inoremap <C-f>  <Right>
inoremap <C-d>  <Delete>
inoremap <expr> <C-a>  indent('.') + 1 < virtcol('.') ? "\<C-o>^" : "\<Home>"
inoremap <expr> <C-e>  indent('.') < virtcol('.') ? "\<End>" : "\<C-o>^"
inoremap <expr> <C-k>  col('.') == col('$') ? "\<Delete>" : "\<C-o>D"
inoremap <expr> <C-y>  pumvisible() ? "\<C-y>" : "\<C-r>\""

" Alternative keys for original actions.
inoremap <C-\>  <C-a>
inoremap <C-q>  <C-d>

" Enable undo for those deletion mappings.
inoremap <C-w>  <C-g>u<C-w>
inoremap <C-u>  <C-g>u<C-u>

" Do complete or indent.
inoremap <expr> <Tab>  pumvisible()
                     \ ? "\<C-n>"
                     \ : <SID>should_complete_rather_than_indent()
                     \ ? <SID>keys_to_complete()
                     \ : "\<C-i>"
inoremap <expr> <S-Tab>  pumvisible()
                       \ ? "\<C-p>"
                       \ : <SID>should_complete_rather_than_indent()
                       \ ? <SID>keys_to_complete()
                       \ : "\<C-i>"

function! s:should_complete_rather_than_indent() abort
  return search('\S\%#', 'Wbn') > 0
endfunction

function! s:keys_to_complete() abort
  if &l:filetype ==# 'vim'
    return "\<C-x>\<C-v>"
  elseif &l:omnifunc != ''
    return "\<C-x>\<C-o>"
  elseif &l:completefunc != ''
    return "\<C-x>\<C-u>"
  else
    return "\<C-n>"
  endif
endfunction

" The <Space>  {{{2

map <Space>  [Space]
noremap [Space]  <Nop>

nnoremap <silent> [Space]w  <Cmd>w<CR>
nnoremap <silent> [Space]W  <Cmd>SudaWrite<CR>

nnoremap <silent> [Space]m  <Cmd>marks<CR>
nnoremap <silent> [Space]r  <Cmd>registers<CR>

" Close temporary windows such as 'quickfix' or 'help'.
nnoremap <silent> [Space]c  <Cmd>call <SID>close_temporary_windows()<CR>

function! s:close_temporary_windows() abort
  let last_winnr = winnr('$')
  if last_winnr <= 1
    return
  endif
  for winnr in range(last_winnr, 1, -1)
    let bufnr = winbufnr(winnr)
    if win_gettype(winnr) != '' ||
    \ (!buflisted(bufnr) && getbufvar(bufnr, '&buftype') != '')
      execute winnr 'wincmd w'
      wincmd c
    endif
  endfor
endfunction

nnoremap [Space]o  <Nop>
nnoremap <silent> [Space]oc  <Cmd>call <SID>toggle_colorcolumn()<CR>
nnoremap <silent> [Space]og  <Cmd>call <SID>toggle_grepprg()<CR>
nnoremap <silent> [Space]ol  <Cmd>call <SID>toggle_option('cursorline')<CR>
nnoremap <silent> [Space]on  <Cmd>call <SID>toggle_option('number')<CR>
nnoremap <silent> [Space]op  <Cmd>call <SID>toggle_option('paste')<CR>
nnoremap <silent> [Space]os  <Cmd>call <SID>toggle_option('spell')<CR>
nnoremap <silent> [Space]ow  <Cmd>call <SID>toggle_option('wrap')<CR>

nnoremap <silent> [Space]/  <Cmd>call <SID>toggle_option('hlsearch')<CR>

function! s:toggle_option(option_name) abort
  execute 'setlocal' a:option_name.'!'
  execute 'setlocal' a:option_name.'?'
endfunction

function! s:toggle_colorcolumn() abort
  if exists('b:textwidth')
    let &l:textwidth = b:textwidth
    unlet b:textwidth
    setlocal colorcolumn& colorcolumn?
  else
    let b:textwidth = &l:textwidth
    if b:textwidth == 0
      setlocal textwidth=80
    endif
    setlocal colorcolumn=+1 colorcolumn?
  endif
endfunction

nnoremap [Space]f  <Nop>
nnoremap [Space]fe  :<C-u>setlocal fileencoding=
nnoremap [Space]ff  :<C-u>setlocal fileformat=
nnoremap [Space]fi  <Cmd>setlocal expandtab? shiftwidth? softtabstop?<CR>
nnoremap [Space]fr  :<C-\>esetcmdpos(8 + strlen(expand('%:p:r'))) ? '' : 'Rename ' . expand("%:p")<CR>
nnoremap [Space]fs  <Cmd>setlocal filetype? fileencoding? fileformat?<CR>
nnoremap [Space]ft  :<C-u>setfiletype<Space>

nnoremap [Space]l  zo
nnoremap [Space]h  zc
nnoremap [Space]H  zM
nnoremap [Space]v  zMzv

nnoremap <silent> [Space].  <Cmd>Source $MYVIMRC<CR>
nnoremap <silent> [Space]%  <Cmd>Source %<CR>

" Put the clipboard content.
nnoremap [Space]p  "+p

" Enter the command-line window.
nnoremap [Space]:  q:
xnoremap [Space]:  q:

" Windows  {{{2

nnoremap <Tab>  <C-w>w
nnoremap <S-Tab>  <C-w>W

nnoremap <C-w>*  <Cmd>Split \| normal! *<CR>
nnoremap <C-w>#  <Cmd>Split \| normal! #<CR>

nnoremap <C-w>f  <Cmd>Split wincmd f<CR>
nnoremap <C-w><C-f>  <Cmd>Split wincmd f<CR>

nnoremap <silent> <C-w>t
\ :call <SID>move_window_into_tabpage(<SID>ask_tabpage_number())<CR>
nmap <C-w><C-t>  <C-w>t

function! s:ask_tabpage_number() abort
  echon 'Which tabpage to move this window into? '

  let c = nr2char(getchar())
  if c =~# '[0-9]'
    return 1 + char2nr(c) - char2nr('0')
  else
    return 0
  endif
endfunction

function! s:move_window_into_tabpage(target_tabpagenr) abort
  " Move the current window into a:target_tabpagenr.
  if a:target_tabpagenr <= 0  " ignore invalid number
    return
  endif
  let original_tabnr = tabpagenr()
  let target_bufnr = bufnr('')
  let window_view = winsaveview()

  if a:target_tabpagenr > tabpagenr('$')
    tabnew
    tabmove  " Move new tabpage at the last
    execute target_bufnr 'buffer'
    let target_tabpagenr = tabpagenr()
  else
    execute a:target_tabpagenr 'tabnext'
    let target_tabpagenr = a:target_tabpagenr
    if winnr('$') > 1 || bufname(winbufnr(0)) != ''
      SplitTop
    endif
    execute target_bufnr 'buffer'
  endif
  call winrestview(window_view)

  execute original_tabnr 'tabnext'
  if winnr('$') > 1
    close
  else
    let target_tabpagenr -= tabpagenr() < target_tabpagenr
    tabclose
  endif

  execute target_tabpagenr 'tabnext'
endfunction

nnoremap <C-w>Q  <Cmd>quit!<CR>

" Markring  {{{2

let s:MARKRING_MARKS = [
\    'a', 'b', 'c', 'd', 'e', 'f', 'g', 'h', 'i', 'j', 'k', 'l', 'm',
\    'n', 'o', 'p', 'q', 'r', 's', 't', 'u', 'v', 'w', 'x', 'y', 'z',
\ ]

nnoremap <expr> <C-@>  <SID>push_into_markring()
nnoremap <expr> <C-Space>  <SID>push_into_markring()

call submode#enter_with('markring', 'nv', '', '``')
call submode#map('markring', 'nv', 'e', '`',
\                s:SID_PREFIX . 'pop_from_markring()')

function! s:push_into_markring() abort
  if !exists('b:markring_position')
    let b:markring_position = 0
  else
    let b:markring_position = (b:markring_position + 1)
    \                         % len(s:MARKRING_MARKS)
  endif
  let mark = s:MARKRING_MARKS[b:markring_position]
  return 'm' . mark
endfunction

function! s:pop_from_markring() abort
  let available_marks = join(map(getmarklist(bufnr('%')), 'v:val.mark[1:]'), '')
  let initial_position = get(b:, 'markring_position', 0)
  let position = initial_position
  while 1
    let mark = s:MARKRING_MARKS[position]
    let position = (len(s:MARKRING_MARKS) + position - 1)
    \              % len(s:MARKRING_MARKS)
    if stridx(available_marks, mark) >= 0
      let b:markring_position = position
      return "'" . mark
    endif
    if position == initial_position
      return ''
    endif
  endwhile
endfunction

" Text objects  {{{2

" Synonym for <>.
onoremap aa  a>
vnoremap aa  a>
onoremap ia  i>
vnoremap ia  i>

" Synonym for [].
onoremap ar  a]
vnoremap ar  a]
onoremap ir  i]
vnoremap ir  i]

" A quoted string without extra white spaces as with a"/a'/a`.
onoremap a'  2i'
vnoremap a'  2i'
onoremap a"  2i"
vnoremap a"  2i"
onoremap a`  2i`
vnoremap a`  2i`

" Select the last changed text.
nnoremap gc  `[v`]h
onoremap gc  <Cmd>normal! gc<CR>
vnoremap gc  <Cmd>normal! gc<CR>

" Select the last selected text.
onoremap <silent> gv  <Cmd>normal! gv<CR>

" Operators  {{{2
" operator-eval  {{{3

call operator#user#define('eval',
\                         s:SID_PREFIX . 'operator_eval')

function! s:operator_eval(motion_wiseness) abort
  let reg_value = getreg('"')
  let reg_type = getregtype('"')
  try
    let visual_command =
    \   operator#user#visual_command_from_wise_name(a:motion_wiseness)
    execute 'normal!' ('`[' . visual_command . '`]""y')
    let result = join(map(split(@", "\n"), "s:sandbox_eval(s:trim(v:val))"), "\n")
    call setreg('"', result, visual_command)
    execute 'normal!' ('`[' . visual_command . '`]""p')
  finally
    call setreg('"', reg_value, reg_type)
  endtry
endfunction

map g=  <Plug>(operator-eval)
nmap g==  <Plug>(operator-eval)<Plug>(operator-eval)

" operator-increment/decrement  {{{3

call operator#user#define('increment',
\                         s:SID_PREFIX . 'operator_increment')
call operator#user#define('decrement',
\                         s:SID_PREFIX . 'operator_decrement')

function! s:operator_increment(motion_wiseness) abort
  let visual_command =
  \   operator#user#visual_command_from_wise_name(a:motion_wiseness)
  execute 'normal!' ('`[' . visual_command . '`]g' . "\<C-a>")
endfunction

function! s:operator_decrement(motion_wiseness) abort
  let visual_command =
  \   operator#user#visual_command_from_wise_name(a:motion_wiseness)
  execute 'normal!' ('`[' . visual_command . '`]g' . "\<C-x>")
endfunction

map g<C-a>  <Plug>(operator-increment)
map g<C-x>  <Plug>(operator-decrement)

" operator-multiply  {{{3

call operator#user#define('multiply',
\                         s:SID_PREFIX . 'operator_multiply')

function! s:operator_multiply(motion_wiseness) abort
  let reg_value = getreg('"')
  let reg_type = getregtype('"')
  try
    let visual_command =
    \   operator#user#visual_command_from_wise_name(a:motion_wiseness)
    execute 'normal!' ('`[' . visual_command . '`]""yP')
  finally
    call setreg('"', reg_value, reg_type)
  endtry
endfunction

map gm  <Plug>(operator-multiply)
nmap gmm  <Plug>(operator-multiply)<Plug>(operator-multiply)

" operator-search-forward/backward  {{{3

call operator#user#define('search-forward',
\                         s:SID_PREFIX . 'operator_search',
\                         'let v:searchforward = 1')
call operator#user#define('search-backward',
\                         s:SID_PREFIX . 'operator_search',
\                         'let v:searchforward = 0')

function! s:operator_search(motion_wiseness) abort
  " v:searchforward will be changed by setreg('/'), so we save it.
  let search_forward = v:searchforward
  let reg_value = getreg('"')
  let reg_type = getregtype('"')

  try
    let visual_command =
    \   operator#user#visual_command_from_wise_name(a:motion_wiseness)
    execute 'normal!' ('`[' . visual_command . '`]""y')

    let pattern = '\V' . substitute(escape(@", '\'), '\n', '\\n', 'g')
    call setreg('/', pattern)
    call histadd('/', pattern)

    execute 'normal!' (search_forward ? 'n' : 'N')
  finally
    call setreg('"', reg_value, reg_type)
  endtry
endfunction

vmap *  <Plug>(operator-search-forward)
vmap #  <Plug>(operator-search-backward)

" operator-source  {{{3

call operator#user#define('source', s:SID_PREFIX . 'operator_source')

function! s:operator_source(motion_wiseness) abort
  let start = getpos("'[")
  let end = getpos("']")
  let range = start[1] . ',' . end[1]
  execute range 'Source'
endfunction

map g.  <Plug>(operator-source)

" operator-translate  {{{3

call operator#user#define('translate', s:SID_PREFIX . 'operator_translate')

function! s:operator_translate(motion_wiseness) abort
  let visual_command =
  \   operator#user#visual_command_from_wise_name(a:motion_wiseness)
  silent execute 'normal!' ('`[' . visual_command . '`]""y')

  let text = @"
  let text = substitute(text, '\n\@<!\n\n\@!', ' ', 'g')
  let text = substitute(text, '\n\+', '\n', 'g')

  if text =~ '[\u3000-\u30ff\u3400-\u4dbf\u4e00-\u9fff\uf900-\ufaff]'
    let translation = google_translate#translate('ja', 'en', text)
  else
    let translation = google_translate#translate('en', 'ja', text)
  endif

  let @" = translation

  echon s:trim(translation) "\n"
endfunction

Arpeggio map ot  <Plug>(operator-translate)

" operator-yank-clipboard  {{{3

call operator#user#define('yank-clipboard',
\                         s:SID_PREFIX . 'operator_yank_clipboard')

function! s:operator_yank_clipboard(motion_wiseness) abort
  let visual_command =
  \   operator#user#visual_command_from_wise_name(a:motion_wiseness)
  execute 'normal!' ('`[' . visual_command . '`]"+y')
endfunction

Arpeggio noremap oy  <Plug>(operator-yank-clipboard)

" Misc.  {{{2

" Command-line helpers.
nnoremap <C-h>  :<C-u>Help<Space>
nnoremap <C-o>  :<C-u>edit<Space>
nnoremap <C-w>.  <Cmd>edit .<CR>

" x/X without register.
nnoremap X  "_X
vnoremap X  "_X
nnoremap x  "_x
vnoremap x  "_x

" Yank from the cursor to the end of the line.
nnoremap Y  y$

" Jump list navigations.
nnoremap <C-j>  <C-i>
nnoremap <C-k>  <C-o>

" Disable dangerous keys.
nnoremap ZZ  <Nop>
nnoremap ZQ  <Nop>

" The shortcut for :update.
nnoremap <silent> <Leader><Leader>  <Cmd>update<CR>

" Like o/O, but insert additional [count] blank lines.
nnoremap <expr> <Plug>(arpeggio-default:o)
\        <SID>start_insert_mode_with_blank_lines('o')
nnoremap <expr> O
\        <SID>start_insert_mode_with_blank_lines('O')

function! s:start_insert_mode_with_blank_lines(command) abort
  if v:count != v:count1
    return a:command
  endif

  if a:command ==# 'o'
    return "\<Esc>o" . repeat("\<CR>", v:count - 1)
  else
    return "\<Esc>O" . repeat("\<CR>\<Up>", v:count - 1) . "\<Esc>S"
  endif
endfunction

" Make search directions consistent.
nnoremap <expr> n  v:searchforward ? 'nzv' : 'Nzv'
nnoremap <expr> N  v:searchforward ? 'Nzv' : 'nzv'
vnoremap <expr> n  v:searchforward ? 'nzv' : 'Nzv'
vnoremap <expr> N  v:searchforward ? 'Nzv' : 'nzv'
onoremap <expr> n  v:searchforward ? 'n' : 'N'
onoremap <expr> N  v:searchforward ? 'N' : 'n'

" Jump to the definition of the keyword under the cursor.
nnoremap <expr> <CR>  &l:filetype ==# 'qf' ? "\<CR>" : "\<C-]>"
vnoremap <expr> <CR>  &l:filetype ==# 'qf' ? "\<CR>" : "\<C-]>"

" A fallback for repeat.
nnoremap <silent> \  .

noremap <C-z>  <Nop>
nnoremap <C-z>  <Cmd>SuspendWithAutomticCD<CR>

noremap <Leader>  <Nop>
noremap <LocalLeader>  <Nop>

" Map a shortcut for :substitute.
nnoremap gs  :<C-u>%s/\V\<<C-r>=<SID>get_cursor_word_pattern()<CR>\>//gI<Left><Left><Left>
vnoremap gs
\ :<C-u>%s/\V\<<C-r>=<SID>get_visual_selection_pattern()<CR>\>//gI<Left><Left><Left>

function! s:get_cursor_word_pattern() abort
  return escape(expand('<cword>'), '/\')
endfunction

function! s:get_visual_selection_pattern() abort
  let lines = getregion(getpos("'<"), getpos("'>"))
  call map(lines, { _, line -> escape(line, '/\') })
  return join(lines, '\\n')
endfunction

" Filetypes  {{{1
" All filetypes   {{{2

autocmd MyAutoCmd FileType *
\ call s:on_FileType_any()

function! s:on_FileType_any() abort
  if &l:completefunc == ''
    setlocal completefunc=git_complete#completefunc
  endif

  if &l:omnifunc == ''
    setlocal omnifunc=syntaxcomplete#Complete
  endif

  " Enable "smartindent" only if filetype is set.
  setlocal smartindent

  " Disable auto-wrapping.
  setlocal formatoptions-=t formatoptions-=c
endfunction

" Optimize the loading for large files.
autocmd MyAutoCmd BufReadPre *
\   if getfsize(expand('<afile>')) > 1024 * 1024 * 8
\ |   set eventignore+=FileType
\ |   setlocal noswapfile
\ | else
\ |   set eventignore-=FileType
\ | endif

" Unset 'paste' automatically.
autocmd MyAutoCmd InsertLeave *  set nopaste

" Highlight special spaces.
autocmd MyAutoCmd VimEnter,WinEnter *
\ match Underlined /[\u1680\u2000-\u200a\u202f\u205f\u3000]/

" Jump to the last cursor position.
autocmd MyAutoCmd BufReadPost *
\   if line("'\"") >= 1 && line("'\"") <= line('$')
\ |   execute "normal! g`\"zv"
\ | endif

" Restore last enabled fold options in the buffer.
autocmd MyAutoCmd BufWinEnter *
\ call s:preserve_fold_options()
autocmd MyAutoCmd OptionSet foldenable,foldexpr,foldmethod,foldminlines,foldtext
\   if v:option_type ==# 'local'
\ |   call s:save_fold_options()
\ | endif

function! s:save_fold_options() abort
  let b:last_fold_options = {
  \   'foldenable': &l:foldenable,
  \   'foldexpr': &l:foldexpr,
  \   'foldmethod': &l:foldmethod,
  \   'foldminlines': &l:foldminlines,
  \   'foldnestmax': &l:foldnestmax,
  \   'foldtext': &l:foldtext
  \ }
endfunction

function! s:preserve_fold_options() abort
  if &l:foldenable && &l:foldmethod ==# 'expr' && &l:foldexpr !=# '0'
    call s:save_fold_options()
  else
    if exists('b:last_fold_options')
      let bufnr = bufnr()
      for [key, value] in items(b:last_fold_options)
        call setbufvar(bufnr, '&' . key, value)
      endfor
    endif
  endif
endfunction

" JSON  {{{2

let g:vim_json_conceal = 0

" PHP  {{{2

let g:PHP_vintage_case_default_indent = 1
let g:PHP_noArrowMatching = 1

" Python  {{{2

let g:python_highlight_all = 1

" Rust  {{{2

let g:rust_recommended_style = 1

let g:cargo_makeprg_params = 'build --all-targets'

" Shell Script  {{{2

let g:is_bash = 1

" Vim Script  {{{2

" Disable continuation line indent.
let g:vim_indent_cont = 0

" Disable Lua embedding, because this is buggy.
let g:vimsyn_embed = ''

" Plugins  {{{1
" accelerate  {{{2

call accelerate#map('nv', 'e', 'j', 'v:count == 0 ? "gj" : "j"')
call accelerate#map('nv', 'e', 'k', 'v:count == 0 ? "gk" : "k"')
call accelerate#map('nv', '', 'h', 'h')
call accelerate#map(
\   'nv',
\   'e',
\   'l',
\   'v:count == 0 && foldclosed(line(".")) >= 0 ? "zo" : "l"'
\ )

" exjumplist  {{{2

nmap <M-C-j>  <Plug>(exjumplist-next-buffer)
nmap <M-C-k>  <Plug>(exjumplist-previous-buffer)

" grex  {{{2

nmap gy  <Plug>(operator-grex-yank)<Plug>(textobj-entire-a)
vmap gy  <Plug>(operator-grex-yank)
nmap gd  <Plug>(operator-grex-delete)<Plug>(textobj-entire-a)
vmap gd  <Plug>(operator-grex-delete)

" luis  {{{2
" Mappings  {{{3

nnoremap [Space]k  <Nop>
nnoremap <silent> [Space]k/  <Cmd>call <SID>luis_start(luis#source#history#new('search'))<CR>
nnoremap <silent> [Space]k:  <Cmd>call <SID>luis_start(luis#source#history#new('cmd'))<CR>
nnoremap <silent> [Space]k;  <Cmd>call <SID>luis_start(luis#source#changelist#new(bufnr('%')))<CR>
nnoremap <silent> [Space]kM  <Cmd>call <SID>luis_start(luis#source#mark#new())<CR>
nnoremap <silent> [Space]kT  <Cmd>call <SID>luis_start(luis#source#tagstack#new(win_getid()))<CR>
nnoremap <silent> [Space]k\  <Cmd>call <SID>luis_start(luis#source#history#new('expr'))<CR>
nnoremap <silent> [Space]ka  <Cmd>call <SID>luis_start(luis#source#arglist#new())<CR>
nnoremap <silent> [Space]kb  <Cmd>call <SID>luis_start_buffer_in_recently_used_order()<CR>
nnoremap <silent> [Space]kc  <Cmd>call <SID>luis_start_with_path(luis#source#file#new(), expand('%:h'))<CR>
nnoremap <silent> [Space]kd  <Cmd>call <SID>luis_start(luis#source#lsp_document_symbol#new(bufnr('%')))<CR>
nnoremap <silent> [Space]kf  <Cmd>call <SID>luis_start(luis#source#file#new())<CR>
nnoremap <silent> [Space]kg  <Cmd>call <SID>luis_start(luis#source#metarw#new('git'))<CR>
nnoremap <silent> [Space]kh  <Cmd>call <SID>luis_start(luis#source#help#import())<CR>
nnoremap <silent> [Space]ki  <Cmd>call <SID>luis_start(luis#source#history#new('input'))<CR>
nnoremap <silent> [Space]kj  <Cmd>call <SID>luis_start(luis#source#jumplist#new(win_getid()))<CR>
nnoremap <silent> [Space]kk  <Cmd>call <SID>luis_resume()<CR>
nnoremap <silent> [Space]km  <Cmd>call <SID>luis_start(luis#source#local_mark#new(bufnr('%')))<CR>
nnoremap <silent> [Space]kn  <Cmd>call <SID>luis_start_file_in_directory(expand("~/Sync/Documents/Notes"))<CR>
nnoremap <silent> [Space]ko  <Cmd>call <SID>luis_start(luis#source#oldfiles#new())<CR>
nnoremap <silent> [Space]kq  <Cmd>call <SID>luis_start(luis#source#quickfix#new())<CR>
nnoremap <silent> [Space]kr  <Cmd>call <SID>luis_start(luis#source#register#new())<CR>
nnoremap <silent> [Space]ks  <Cmd>call <SID>luis_start(luis#source#spell#import())<CR>
nnoremap <silent> [Space]kw  <Cmd>call <SID>luis_start_workspace(expand('~/Sync/works'))<CR>
nnoremap <silent> [Space]kz  <Cmd>call <SID>luis_start(luis#source#fold#new(win_getid()))<CR>

if executable('fzf-filter')
  nnoremap <silent> [Space]kl  <Cmd>call <SID>luis_start_async_files()<CR>
  nnoremap <silent> [Space]kt  <Cmd>call <SID>luis_start_async_tags()<CR>
endif

let s:luis_last_session = 0

function! s:luis_resume() abort  " {{{3
  if s:luis_last_session isnot 0
    call luis#start(s:luis_last_session)
  endif
endfunction

function! s:luis_start(source, ...) abort  " {{{3
  let options = get(a:000, 0, {})
  let session = luis#new_session(a:source, options)
  if luis#start(session)
    let s:luis_last_session = session
  endif
endfunction

function! s:luis_start_async_files() abort  " {{{3
  if isdirectory('.git')
    let command = [
    \   'fzf-filter', '-f', '1', '-p', '2', '-d', ' ', '-l', '1000',
    \   '--',
    \   'git', 'ls-files', '--exclude-standard', '-v', '-c', '-o',
    \ ]
    let options = {
    \   'to_candidate': function('s:to_git_file_candidate'),
    \ }
    let source = luis#source#async#new(
    \   'git-files',
    \   luis#kind#file#import(),
    \   command,
    \   options
    \ )
  else
    let command = [
    \   'fzf-filter', '-l', '1000',
    \   '--',
    \   'fd', '--type', 'f', '--strip-cwd-prefix', '-c', 'never', '--max-results', '10000',
    \ ]
    let options = {
    \   'to_candidate': { line -> {
    \     'word': line,
    \     'user_data': { 'preview_path': line },
    \   }},
    \ }
    let source = luis#source#async#new(
    \   'fd',
    \   luis#kind#file#import(),
    \   command,
    \   options
    \ )
  endif

  call s:luis_start(source)
endfunction

function! s:luis_start_async_tags() abort  " {{{3
  let name = 'tags'
  let kind = luis#kind#tag#import()
  let command = [
  \   'fzf-filter', '-l', '1000', '-f', '0',
  \   '--',
  \   'sh', '-c', 'echo "$@" | xargs -n 1 -P 4 cat', '',
  \ ]
  call extend(command, tagfiles())
  let options = {
  \   'to_candidate': function('s:to_tag_candidate'),
  \ }
  let source = luis#source#async#new(name, kind, command, options)
  return s:luis_start(source)
endfunction

function! s:luis_start_buffer_in_recently_used_order() abort  " {{{3
  let source = luis#source#buffer#new({
  \   'sort_priority': { bufinfo -> -bufinfo.lastused },
  \ })
  return s:luis_start(source)
endfunction

function! s:luis_start_file_in_directory(path) abort  " {{{3
  let source = luis#source#file#new()
  let hook = { 'path': a:path }

  function! hook.on_source_enter(context) abort dict
    lcd `=self.path`
  endfunction

  function! hook.on_source_leave(context) abort dict
    lcd -
  endfunction

  call s:luis_start(source, { 'hook': hook })
endfunction

function! s:luis_start_with_path(source, path) abort  " {{{3
  let initial_pattern = a:path != '' && a:path != '.'
  \                   ? a:path . '/'
  \                   : ''
  call s:luis_start(a:source, {
  \   'initial_pattern': initial_pattern,
  \ })
endfunction

function! s:luis_start_workspace(path) abort  " {{{3
  let Callback = function('s:luis_workspace_callback')
  let source = luis#source#workspace#new(a:path, Callback)
  call s:luis_start(source)
endfunction

function! s:luis_workspace_callback(path)  " {{{3
  tcd `=a:path`
  call s:luis_start_async_files()
endfunction

function! s:to_git_file_candidate(line) abort  " {{{3
  let components = split(a:line, '^\S\zs\s')
  let type = get(components, 0, '?')
  let path = get(components, 1, '')
  return {
  \   'word': path,
  \   'kind': type,
  \   'user_data': {
  \     'preview_path': path,
  \   },
  \ }
endfunction

function! s:to_tag_candidate(line) abort  " {{{3
  let components = split(a:line, '\t')
  return {
  \   'word': get(components, 0, a:line),
  \   'menu': get(components, 1, ''),
  \   'dup': 1,
  \ }
endfunction

" netrw  {{{2

let g:netrw_banner = 0
let g:netrw_liststyle = 3

" operator-camelize  {{{2

map <Leader>c  <Plug>(operator-camelize)
map <Leader>C  <Plug>(operator-decamelize)

" operator-comment  {{{2

Arpeggio map oc  <Plug>(operator-comment)
Arpeggio map od  <Plug>(operator-uncomment)

" operator-replece  {{{2

Arpeggio map or  <Plug>(operator-replace)

" operator-sort  {{{2

nmap <Leader>s  <Plug>(operator-sort-numeric)
vmap <Leader>s  <Plug>(operator-sort-numeric)
nmap <Leader>S  <Plug>(operator-sort-numeric)$
vmap <Leader>S  <Plug>(operator-sort-numeric)$

" quickrun  {{{2

command! -complete=command -nargs=+ Capture  QuickRun vim/source -src <q-args>

let g:quickrun_config = {
\   '_': {
\     'outputter/buffer/opener': 'Split',
\   },
\   'objc': {
\     'type': 'objc/clang',
\   },
\   'objc/clang': {
\     'cmdopt': '-framework Foundation -framework AppKit',
\     'command': 'clang',
\     'exec': ['%c %o -o %s:p:r %s', '%s:p:r %a'],
\     'hook/sweep/files': '%S:p:r',
\     'tempfile': '%{fnamemodify(tempname(), ":r")}.m',
\   },
\   'cpp': {
\     'type': 'cpp/g++',
\     'cmdopt': '-std=c++2b',
\   },
\   'dot': {
\     'exec': ['%c -Tsvg -o %s:p:r.svg %s'],
\   },
\   'haskell': {
\     'type': 'haskell/runghc',
\   },
\   'haskell/runghc': {
\     'cmdopt': '--ghc-arg=-Wall -Wno-unused-top-binds',
\     'command': 'runghc',
\     'hook/eval/template': 'main = print \$ %s',
\     'tempfile': '%{tempname()}.hs',
\   },
\   'javascript': {
\     'type': 'javascript/nodejs',
\   },
\   'javascript/nodejs': {
\     'command': 'node',
\     'hook/eval/template': 'console.log(%s)',
\     'tempfile': '%{tempname()}.mjs',
\   },
\   'javascript/browserjs': {
\     'command': 'browserjs',
\     'hook/eval/template': 'console.log(%s)',
\   },
\   'json': {
\     'cmdopt': '.',
\     'command': 'jq',
\   },
\   'lua': {
\     'type': executable('nvim') ? 'lua/neovim' : 'lua/vim',
\   },
\   'lua/neovim': {
\     'cmdopt': '-u NONE -i NONE -N -n -E',
\     'command': 'nvim',
\     'exec': ['%c %o -l %s'],
\     'hook/eval/template': 'print(vim.inspect(%s))',
\     'tempfile': '%{tempname()}.lua',
\   },
\   'rust': {
\     'command': 'rustc',
\     'cmdopt': '-A dead_code --edition 2024',
\     'exec': ['%c %o %s -o %s:p:r', 'RUST_BACKTRACE=1 %s:p:r %a'],
\     'tempfile': '%{fnamemodify(tempname(), ":r")}.rs',
\     'hook/shebang/enable': 0,
\     'hook/sweep/files': '%S:p:r',
\   },
\   'sql': {
\     'type': 'sql/mysql',
\   },
\   'sql/mysql': {
\     'command': 'mysql',
\     'exec': ['%c --host 127.0.0.1 --user root %a < %s'],
\   },
\   'typescript': {
\     'type': 'typescript/tsx',
\   },
\   'typescript/tsx': {
\     'command': 'tsx',
\     'exec': ['%c %o %s %a'],
\     'tempfile': '%{fnamemodify(tempname(), ":r")}.ts',
\   },
\   'vim': {
\     'type': 'vim/sandbox',
\   },
\   'vim/sandbox': {
\     'cmdopt': '-u NONE -i NONE -N -n -E -s',
\     'command': v:progpath,
\     'exec': ['%c %o --cmd "verbose source %S" --cmd "qall!"'],
\     'hook/eval/template': 'echo %s',
\     'tempfile': '%{tempname()}.vim',
\   },
\   'xdefaults': {
\     'command': 'xrdb',
\     'exec': ['%c -remove', '%c -merge %s', '%c -query'],
\   },
\ }

nmap <Leader>r  <Plug>(quickrun)
vmap <Leader>r  <Plug>(quickrun)

" ref  "{{{2

autocmd MyAutoCmd FileType ref
\ call s:on_FileType_ref()

function! s:on_FileType_ref() abort
  nmap <buffer> <silent> <CR>  <Plug>(ref-keyword)
  vmap <buffer> <silent> <CR>  <Plug>(ref-keyword)
  nmap <buffer> <silent> <C-]>  <Plug>(ref-keyword)
  vmap <buffer> <silent> <C-]>  <Plug>(ref-keyword)
  nmap <buffer> <silent> <C-j>  <Plug>(ref-forward)
  nmap <buffer> <silent> <C-k>  <Plug>(ref-back)
  nnoremap <buffer> q  <C-w>c
endfunction

nmap <silent> K  <Plug>(ref-keyword)
vmap <silent> K  <Plug>(ref-keyword)

nnoremap <silent> <Leader>a  :<C-u>call ref#jump('normal', 'alc')<CR>
vnoremap <silent> <Leader>a  :<C-u>call ref#jump('visual', 'alc')<CR>

AlterCommand ref  Ref

let g:ref_no_default_key_mappings = 1
let g:ref_open = 'Split'
let g:ref_perldoc_complete_head = 1
let g:ref_phpmanual_path = '/usr/share/php-docs/en/php-chunked-xhtml'

" scratch  {{{2

nmap [Space]s  <Plug>(scratch-open)

autocmd MyAutoCmd User PluginScratchInitializeAfter
\ call s:on_PluginScratchInitializeAfter()

function! s:on_PluginScratchInitializeAfter() abort
  setlocal nobuflisted
  map <buffer> <CR>  <Plug>(scratch-evaluate!)
endfunction

let g:scratch_show_command = 'SplitTop | hide buffer'

" skeleton  {{{2

autocmd MyAutoCmd User PluginSkeletonDetect
\ call s:on_PluginSkeletonDetect()

function! s:on_PluginSkeletonDetect() abort
  let components = split(expand('%:p'), '/')
  if len(components) == 0
    return
  endif

  let filename = components[-1]
  let directories = components[:-2]

  if filename =~# 'LICENSE'
    SkeletonLoad license-mit
  elseif filename =~# '\.user\.js$'
    SkeletonLoad userjs
  elseif filename =~# 'Cargo\.toml$'
    SkeletonLoad cargo
  elseif filename =~# '\.vim$'
    if get(directories, -2) ==# "after" && get(directories, -1) ==# "ftplugin"
      execute 'SkeletonLoad' 'vim-after-'.directories[-1]
    elseif get(directories, -1) ==# '^\v(compiler|ftdetect|ftplugin|indent|plugin|syntax)$'
      execute 'SkeletonLoad' 'vim-'.directories[-1]
    endif
  endif
endfunction

autocmd MyAutoCmd User PluginSkeletonLoad
\ call s:on_PluginSkeletonLoad()

function! s:on_PluginSkeletonLoad() abort
  silent %s/<%=\s*\(.\{-}\)\s*%>/\=eval(submatch(1))/ge
  if search('<%|%>', 'w')
    normal! "_da>
  else
    normal! gg
  endif
  if foldclosed(line('.'))
    normal! zv
  endif
endfunction

" smartinput  {{{2
" Initialize  {{{3

let g:smartinput_no_default_key_mappings = 1

if exists('g:loaded_smartinput')
  call smartinput#clear_rules()
  call smartinput#define_default_rules()
endif

" Common {{{3

" Insert a multi-line comment.
call smartinput#define_rule({
\   'at': '/\*\*\%#',
\   'char': '<CR>',
\   'input': '*/<Left><Left><CR><CR><BS><BS><Up><Right><Space>',
\ })
call smartinput#define_rule({
\   'at': '/\*\*\%#',
\   'char': '<Space>',
\   'input': '<Space><Space>*/<Left><Left><Left>',
\ })

" for Vim  {{{3

call smartinput#define_rule({
\   'at': '\%#',
\   'char': '"',
\   'input': '"',
\   'filetype': ['vim'],
\   'syntax': ['Comment'],
\ })
call smartinput#define_rule({
\   'at': '\%#',
\   'char': '{',
\   'input': '{',
\   'filetype': ['vim'],
\   'syntax': ['Comment'],
\ })
call smartinput#define_rule({
\   'at': '^[ \t:]*".*\%#$',
\   'char': '"',
\   'input': '"',
\   'filetype': ['vim'],
\ })
call smartinput#define_rule({
\   'at': '^[ \t:]*".*\%#$',
\   'char': '{',
\   'input': '{',
\   'filetype': ['vim'],
\ })

" for PHP  {{{3

call smartinput#define_rule({
\   'at': '\%#',
\   'char': '@',
\   'input': '$this->',
\   'filetype': ['php'],
\ })
call smartinput#define_rule({
\   'at': '\%#[$A-Za-z]',
\   'char': '@',
\   'input': '@',
\   'filetype': ['php'],
\ })
call smartinput#define_rule({
\   'at': '\%#',
\   'char': '@',
\   'input': '@',
\   'filetype': ['php'],
\   'syntax': ['Comment', 'Constant', 'None'],
\ })

" for Rust  {{{3

call smartinput#define_rule({
\   'at': '\%#',
\   'char': "'",
\   'input': "'",
\   'filetype': ['rust'],
\ })

" Fin.  {{{3

call smartinput#map_trigger_keys()

" smartword  {{{2

map w  <Plug>(smartword-w)
map b  <Plug>(smartword-b)
map e  <Plug>(smartword-e)
map ge  <Plug>(smartword-ge)

" submode  {{{2

call submode#enter_with('scroll', 'nv', '', '[Space]j')
call submode#map('scroll', 'nv', 'e', 'j',
\                'line(".") != line("$") ? "<C-d>" : ""')
call submode#map('scroll', 'nv', 'e', 'k',
\                'line(".") != 1 ? "<C-u>" : ""')

call submode#enter_with('undo/redo', 'n', '', 'g-', 'g-')
call submode#enter_with('undo/redo', 'n', '', 'g+', 'g+')
call submode#map('undo/redo', 'n', '', '-', 'g-')
call submode#map('undo/redo', 'n', '', '+', 'g+')

call submode#enter_with(
\   'winsize', 'n', '', '<C-w><Space>',
\   '<Cmd>call ' . s:SID_PREFIX . 'submode_winsize()<CR>'
\ )
call submode#enter_with(
\   'winsize', 'n', '', '<C-w><C-@>',
\   '<Cmd>call ' . s:SID_PREFIX . 'submode_winsize()<CR>'
\ )

function! s:submode_winsize() abort
  let current = winnr()
  wincmd k | let above = winnr() | execute current 'wincmd w'
  wincmd j | let below = winnr() | execute current 'wincmd w'
  wincmd h | let left = winnr() | execute current 'wincmd w'
  wincmd l | let right = winnr() | execute current 'wincmd w'

  execute printf('call submode#map("winsize", "n", "r", "j", "<C-w>%s")',
  \              above != below && current == below ? "-" : "+")
  execute printf('call submode#map("winsize", "n", "r", "k", "<C-w>%s")',
  \              above != below && current == below ? "+" : "-")
  execute printf('call submode#map("winsize", "n", "r", "h", "<C-w>%s")',
  \              left != right && current == right ? ">" : "<")
  execute printf('call submode#map("winsize", "n", "r", "l", "<C-w>%s")',
  \              left != right && current == right ? "<" : ">")
endfunction

let g:submode_timeout = 0

" surround-obj  {{{2

map s  <Plug>(surround-obj-add)
map S  <Plug>(surround-obj-add)$
nmap cs  <Plug>(surround-obj-change)
nmap ds  <Plug>(surround-obj-delete)

let g:surround_obj_objects = {
\   'E': { 'type': 'inline', 'delimiter': '**' },
\   'a': { 'type': 'block', 'delimiter': ['<', '>'] },
\   'e': { 'type': 'inline', 'delimiter': '_' },
\   'r': { 'type': 'block', 'delimiter': ['[', ']'] },
\   'jA': {'type': 'block', 'delimiter': ['', '']},
\   'ja': {'type': 'block', 'delimiter': ['', '']},
\   'jb': {'type': 'block', 'delimiter': ['', '']},
\   'jB': {'type': 'block', 'delimiter': ['', '']},
\   'jk': {'type': 'block', 'delimiter': ['', '']},
\   'jK': {'type': 'block', 'delimiter': ['', '']},
\   'jr': {'type': 'block', 'delimiter': ['', '']},
\   'js': {'type': 'block', 'delimiter': ['', '']},
\   'jt': {'type': 'block', 'delimiter': ['', '']},
\   'jy': {'type': 'block', 'delimiter': ['', '']},
\   'jY': {'type': 'block', 'delimiter': ['', '']},
\ }

" table-mode  {{{2

nnoremap <silent> [Space]ot  <Cmd>TableModeToggle<CR>

" Fin.  {{{1

set secure

" __END__  {{{1
" vim: expandtab softtabstop=2 shiftwidth=2
" vim: foldmethod=marker
